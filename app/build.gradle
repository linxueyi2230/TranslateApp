buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

//apply plugin: 'com.neenbedankt.android-apt'
//apply plugin: 'me.tatarka.retrolambda'

//格式化打完完成后APK名称中的时间
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC+8"))
}

def cfg = rootProject.ext.configuration
def libs = rootProject.ext.libraries;
def test = rootProject.ext.testingLibraries;


android {
    compileSdkVersion cfg.compileVersion
    buildToolsVersion cfg.buildToolsVersion

    defaultConfig {
        applicationId cfg.package
        minSdkVersion cfg.minSdk
        targetSdkVersion cfg.targetSdk
        versionCode cfg.version_code
        versionName cfg.version_name

        flavorDimensions "versionCode"

        //for use support vectorDrawables
        vectorDrawables.useSupportLibrary = true

        //baidu config
        buildConfigField "String", "BAIDU_SCREAT_KEY", '"5jlBZtaMuo6Xlx5LOIVC"'
        buildConfigField "String", "BAIDU_APP_ID", '"20151224000008231"'

        //iciba config
        buildConfigField "String", "ICIBA_KEY", '"8B660C51C6D4A410DE9D406667729BC4"'

        //youdao config
        buildConfigField "String", "YOUDAO_USERNAME", '"gudong"'
        buildConfigField "String", "YOUDAO_KEY", '"1235023502"'
        buildConfigField "String", "YOUDAO_TYPE", '"data"'
        buildConfigField "String", "YOUDAO_DOC_TYPE", '"json"'
        buildConfigField "String", "YOUDAO_VERSION", '"1.2"'

        //google config
        buildConfigField "String", "GOOGLE_LANGUAGE_ENGLISH", '"en"'
        buildConfigField "String", "GOOGLE_LANGUAGE_CHINEASE", '"zh-CN"'

        //common value
        buildConfigField "String", "RESULT_JSON", '"json"'
        buildConfigField "String", "LANGUAGE_AUTO", '"auto"'
        buildConfigField "Boolean", "IS_DEBUG", 'true'

        resValue "string", "tray__authority", "${cfg.package}.tray"

//        manifestPlaceholders = [
//                jpush_appkey : "f4bae95c7c5d1f16e2e16664"
//        ]
        manifestPlaceholders = [
                jpush_appkey : "d07edc4b579c9c548be0e957"
        ]
    }

    signingConfigs {
        //指定名叫release的一个签名对象，下面会动态根据配置文件设置属性
        releaseConfig
    }

    buildTypes {
        debug {
            //applicationIdSuffix ".debug"
            debuggable true
            resValue "string", "tray__authority", "${cfg.package}.debug.tray"
        }
        release {
            if (signingConfigs.releaseConfig != null) {
                signingConfig signingConfigs.releaseConfig
            }
            //设置是否混淆
            minifyEnabled false
            //设置压缩对齐
            zipAlignEnabled false
            //移除无用的资源文件
            shrinkResources false
            //设置混淆配置文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //指定签名包得输入文件名称
            buildConfigField "Boolean", "IS_DEBUG", 'false'
        }
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        beta {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "beta"]
        }
        xiaomi {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "xiaomi"]
        }
        m360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "m360"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
        fir {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "fir"]
        }
        play {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "play"]
        }
        coolapk {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "coolapk"]
        }
        dev21 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "dev21"]
            minSdkVersion cfg.minSdk21
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

//set release sign info synchronously
def Properties props = new Properties()
def propFile = file('../local.properties')
if (propFile.exists()) {
    props.load(new FileInputStream(propFile))
    if (props != null &&
            props.containsKey('SIGN_FILE') && props.containsKey('SIGN_KEYSTORE_PASS') &&
            props.containsKey('SIGN_KEYSTORE_ALIAS') && props.containsKey('SIGN_KEYSTORE_ALIAS_PASS')) {
        println 'release build signing'
        android.signingConfigs.releaseConfig.storeFile = file(props['SIGN_FILE'])
        android.signingConfigs.releaseConfig.storePassword = props['SIGN_KEYSTORE_PASS']
        android.signingConfigs.releaseConfig.keyAlias = props['SIGN_KEYSTORE_ALIAS']
        android.signingConfigs.releaseConfig.keyPassword = props['SIGN_KEYSTORE_ALIAS_PASS']
    } else {
        println 'release build not found signing properties'
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'release build not found signing file'
    android.buildTypes.release.signingConfig = null
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Test
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation "junit:junit:${test.junit}"
    // Android
    implementation "com.android.support:recyclerview-v7:${libs.supportVersion}"
    implementation "com.android.support:appcompat-v7:${libs.supportVersion}"
    implementation "com.android.support:design:${libs.supportVersion}"

    // Google
    implementation "com.google.dagger:dagger:${libs.dagger}"
    annotationProcessor "com.google.dagger:dagger-compiler:${libs.dagger}"
    compileOnly "org.glassfish:javax.annotation:${libs.javax_annotation}"
    // Square
    implementation "com.jakewharton:butterknife:${libs.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${libs.butterknife}"
    implementation "com.squareup.retrofit2:retrofit:${libs.retrofit}"
    implementation "com.squareup.retrofit2:converter-gson:${libs.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${libs.retrofit}"
    // Reactive
    implementation "io.reactivex:rxandroid:${libs.rxandroid}"
    // Other
    implementation "com.orhanobut:logger:${libs.logger}"
    //retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:${libs.retrolambda}"
    implementation "com.jonathanfinerty.once:once:${libs.once}"

    implementation "com.android.support:preference-v7:${libs.supportVersion}"
    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation "com.android.support:support-v4:${libs.supportVersion}"

    implementation "moe.feng:AlipayZeroSdk:${libs.alipay_zero_sdk}"

    implementation "com.jenzz:materialpreference:${libs.materialpreference}"
    implementation "com.github.bumptech.glide:glide:${libs.glide}"

    implementation("me.drakeet.library:crashwoodpecker:${libs.crashwoodpecker}") {
        exclude module: 'recyclerview-v7'
    }

    implementation "com.readystatesoftware.sqliteasset:sqliteassethelper:${libs.sqliteassethelper}"

    implementation "net.grandcentrix.tray:tray:${libs.tray}"

    implementation "com.facebook.stetho:stetho:${libs.stetho}"
    implementation "com.facebook.stetho:stetho-okhttp3:${libs.stetho_okhttp3}"

    implementation('me.drakeet.support:about:2.0.2', {
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    })
    implementation('me.drakeet.multitype:multitype:3.3.3', {
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    })

//    implementation ('me.drakeet.support:about-extension:2.0.2', {
//        exclude module: 'recyclerview-v7'
//        exclude module: 'appcompat-v7'
//        exclude module: 'design'
//    })
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true;
    }

    implementation 'com.github.anzewei:parallaxbacklayout:1.1.9'
    implementation 'com.airbnb.android:lottie:2.1.0'
    //implementation project(':shadow6.0')
    implementation (name:'shadow6.2',ext:'aar')
}